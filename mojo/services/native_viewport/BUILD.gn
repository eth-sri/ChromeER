# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/ui.gni")

component("native_viewport") {
  output_name = "mojo_native_viewport"

  deps = [
    "//base",
    "//ui/events",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//mojo/public/cpp/application:chromium",
    "//mojo/common",
    "//mojo/environment:chromium",
    "//mojo/services/public/cpp/geometry",
    "//mojo/services/public/interfaces/geometry",
    "//mojo/services/gles2",
    "//mojo/services/public/cpp/input_events",
    "//mojo/services/public/interfaces/native_viewport",
  ]

  defines = [ "MOJO_NATIVE_VIEWPORT_IMPLEMENTATION" ]

  sources = [
    "native_viewport.h",
    "native_viewport_android.cc",
    "native_viewport_mac.mm",
    "native_viewport_service.cc",
    "native_viewport_service.h",
    "native_viewport_win.cc",
  ]

  if (is_ios) {
    sources += [ "native_viewport_stub.cc" ]
  }

  if (is_android) {
    deps += [ "//mojo:jni_headers" ]
  }

  if (is_win) {
    deps += [
      # TODO(GYP)
      # '../ui/platform_window/win/win_window.gyp:win_window',
    ]
  }

  if (use_x11) {
    sources += [ "native_viewport_x11.cc" ]
    deps += [ "//ui/platform_window/x11" ]
  }

  if (use_ozone) {
    sources += [ "native_viewport_ozone.cc" ]
  }

  if (is_component_build) {
    deps += [ "//mojo/system" ]
  }
}
